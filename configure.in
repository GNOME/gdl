AC_INIT(gdlConf.sh.in)
AM_PROG_XML_I18N_TOOLS

dnl ==========================================================================
dnl
dnl If you add a version number here, you *must* add an AC_SUBST line for
dnl it too, or it will never make it into the spec file!
dnl
dnl ==========================================================================

GTK_REQUIRED=1.2.0
GNOME_LIBS_REQUIRED=1.2.9
BONOBO_REQUIRED=1.0.7
OAF_REQUIRED=0.6.5
GNOME_VFS_REQUIRED=1.0
LIBXML_REQUIRED=1.8.15

AC_SUBST(GTK_REQUIRED)
AC_SUBST(GNOME_LIBS_REQUIRED)
AC_SUBST(BONOBO_REQUIRED)
AC_SUBST(OAF_REQUIRED)
AC_SUBST(GNOME_VFS_REQUIRED)
AC_SUBST(LIBXML_REQUIRED)

dnl ==========================================================================

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gdl,0.1.0)

AM_ACLOCAL_INCLUDE(macros)

AM_MAINTAINER_MODE

dnl
dnl GDL_CHECK_LIB is defined in gdl.m4.  However, we can't use that yet.
dnl So we have a copy here.  If you modify this, see if you should modify
dnl the one in gdl.m4 too.
dnl
AC_DEFUN(CHECK_LIB, [
	dispname="$1"
	pkgname="$2"
	minvers="$3"
	maxvers="$4"

	AC_MSG_CHECKING(for $dispname)

	if gnome-config --libs $pkgname > /dev/null 2>&1; then
		pkgvers=`gnome-config --modversion $pkgname | sed -e 's/^[[^0-9]]*//'`
	else
		pkgvers=not
	fi
	AC_MSG_RESULT($pkgvers found)

	pkgvers=`echo $pkgvers | awk -F. '{ print $[]1 * 1000000 + $[]2 * 10000 + $[]3 * 100 + $[]4;}'`
	cmpminvers=`echo $minvers | awk -F. '{ print $[]1 * 1000000 + $[]2 * 10000 + $[]3 * 100 + $[]4;}'`
	cmpmaxvers=`echo $maxvers | awk -F. '{ print $[]1 * 1000000 + $[]2 * 10000 + $[]3 * 100 + $[]4;}'`
	ok=yes
	if test "$pkgvers" -lt $cmpminvers; then
		ok=no
	elif test -n "$maxvers" -a "$pkgvers" -ge $cmpmaxvers; then
		ok=no
	fi
	if test $ok = no; then
		case $maxvers in
		"")
			dispvers="$minvers or higher"
			;;
		$minvers)
			dispvers="$minvers (exactly)"
			;;
		*)
			dispvers="$minvers or higher, but less than $maxvers,"
			;;
		esac

		AC_MSG_ERROR([
""
"You need $dispname $dispvers to build GDL"
"If you think you already have this installed, consult the README."])
	fi

	tmp_bsnom=`echo $pkgname | tr a-z A-Z`
	eval $tmp_bsnom'_CFLAGS'=\"`gnome-config --cflags $pkgname`\"
	eval $tmp_bsnom'_LIBS'=\"`gnome-config --libs $pkgname`\"
])

dnl libtool versioning

dnl gdl-0.1.0 = 0
dnl increment if any interfaces have been added; set to 0
dnl  if any interfaces have been removed. removal has 
dnl  precedence over adding, so set to 0 if both happened.
dnl AKA Major
GDL_AGE=0

dnl gdl-0.1.0 = 1
dnl increment any time the source changes; set to 
dnl  0 if you increment CURRENT
dnl AKA Minor
GDL_REVISION=1

dnl gdl-0.1.0 = 1
dnl increment if the interface has additions, changes, removals.
dnl AKA Micro
GAL_CURRENT=1

AC_SUBST(GDL_CURRENT)
AC_SUBST(GDL_REVISION)
AC_SUBST(GDL_AGE)

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CXX
AC_PROG_YACC
AM_PROG_LEX
AC_STDC_HEADERS
AC_ARG_PROGRAM
AM_PROG_LIBTOOL


AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_INIT()
GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS

dnl ***************
dnl GNOME Libraries 
dnl ***************

CHECK_LIB(gnome-libs, gnome, $GNOME_LIBS_REQUIRED)
CHECK_LIB(Bonobo, bonobo, $BONOBO_REQUIRED)
CHECK_LIB(Bonobo X, bonobox, $BONOBO_REQUIRED)
CHECK_LIB(OAF, oaf, $OAF_REQUIRED)
CHECK_LIB(GNOME-VFS, vfs, $GNOME_VFS_REQUIRED)
CHECK_LIB(libxml, xml, $LIBXML_REQUIRED)

IDL_CFLAGS=`gnome-config --cflags idl`

AC_SUBST(BONOBO_CFLAGS)
AC_SUBST(BONOBO_LIBS)
AC_SUBST(BONOBOX_CFLAGS)
AC_SUBST(BONOBOX_LIBS)
AC_SUBST(OAF_CFLAGS)
AC_SUBST(VFS_CFLAGS)
AC_SUBST(VFS_LIBS)
AC_SUBST(IDL_CFLAGS)
AC_SUBST(XML_CFLAGS)
AC_SUBST(XML_LIBS)

GNOME_IDL_DIR="`$GNOME_CONFIG --datadir`/idl"

AM_GNOME_GETTEXT
ALL_LINGUAS="it ru sv"
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

GDL_LIBDIR='-L${libdir}'
GDL_INCLUDEDIR=" -I${includedir} `$GNOME_CONFIG --cflags gnomeui ` $UNICODE_LIBS"
GDL_LIBS="-lgdl `$GNOME_CONFIG --libs bonobox`"
AC_SUBST(GDL_LIBDIR)
AC_SUBST(GDL_INCLUDEDIR)
AC_SUBST(GDL_LIBS)

AC_OUTPUT([
Makefile
gdl/Makefile
idl/Makefile
scintilla-control/Makefile
scintilla-control/scintilla/Makefile
intl/Makefile
po/Makefile.in 
stamp.h
gdl.spec
],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
