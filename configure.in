dnl ==========================================================================
dnl
dnl If you add a version number here, you *must* add an AC_SUBST line for
dnl it too, or it will never make it into the spec file!
dnl
dnl ==========================================================================

AC_PREREQ(2.65)
AC_INIT(gdl)
AC_CONFIG_MACRO_DIR([m4])
AM_CONFIG_HEADER(config.h)

GDL_MAJOR_VERSION=3
GDL_MINOR_VERSION=7
GDL_MICRO_VERSION=3

dnl increment if the interface has additions, changes, removals.
GDL_CURRENT=5

dnl increment any time the source changes; set to
dnl  0 if you increment CURRENT
GDL_REVISION=5

dnl increment if any interfaces have been added; set to 0
dnl  if any interfaces have been changed or removed. removal has
dnl  precedence over adding, so set to 0 if both happened.
GDL_AGE=0

AC_SUBST(GDL_CURRENT)
AC_SUBST(GDL_REVISION)
AC_SUBST(GDL_AGE)

PACKAGE=gdl
GDL_VERSION=$GDL_MAJOR_VERSION.$GDL_MINOR_VERSION.$GDL_MICRO_VERSION

AM_INIT_AUTOMAKE($PACKAGE, $GDL_VERSION, [1.10 dist-xz no-dist-gzip])

dnl with higher autotools, we can set ACLOCAL_AMFLAGS instead, which is
dnl the better solution; this does the trick for automake-1.4
dnl gnome-autogen.sh sets ACLOCAL_FLAGS, which points to the cvs gnome
dnl autoconf macros
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AC_SUBST(GDL_MAJOR_VERSION)
AC_SUBST(GDL_MINOR_VERSION)
AC_SUBST(GDL_MICRO_VERSION)
AC_SUBST(GDL_VERSION)

SNAPSHOT=0_`date +%Y%m%d_%H%M%S`_git
AC_SUBST(SNAPSHOT)

GTK_REQUIRED=3.0.0
XML_REQUIRED=2.2.8

AC_SUBST(GTK_REQUIRED)
AC_SUBST(XML_REQUIRED)

AM_MAINTAINER_MODE([enable])
m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

AC_ISC_POSIX
AC_PROG_CC

AC_CHECK_HEADERS([libintl.h])
AC_TYPE_SIGNAL

AC_PROG_CXX
AC_STDC_HEADERS
AM_SANITY_CHECK

AC_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL


dnl ***************
dnl GTK Libraries 
dnl ***************

PKG_CHECK_MODULES(XML, libxml-2.0 >= $XML_REQUIRED)
AC_SUBST(XML_CFLAGS)
AC_SUBST(XML_LIBS)

PKG_CHECK_MODULES(GDL_DEPENDENCIES, 
	gtk+-3.0 >= $GTK_REQUIRED)
AC_SUBST(GDL_DEPENDENCIES_CFLAGS)
AC_SUBST(GDL_DEPENDENCIES_LIBS)

dnl if test x$MAINT = x; then
dnl 	DEPRECATED_FLAGS="-DGDK_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED -DGNOME_DISABLE_DEPRECATED -DBONOBO_DISABLE_DEPRECATED -DBONOBO_UI_DISABLE_DEPRECATED"
dnl	AC_SUBST(DEPRECATED_FLAGS)
dnl fi

AC_MSG_CHECKING([for native Win32])
case "$host" in
  *-*-mingw*)
    native_win32=yes
    ;;
  *)
    native_win32=no
    ;;
esac
AC_MSG_RESULT([$native_win32])
AM_CONDITIONAL(OS_WIN32, test "x$native_win32" = "xyes")

EXTRA_CPPFLAGS=
EXTRA_LDFLAGS="-no-undefined"
if test "x$native_win32" = "xyes"; then
  EXTRA_CPPFLAGS="-mms-bitfields"
  EXTRA_LDFLAGS="$EXTRA_LDFLAGS -Wl,--kill-at -Wl,--export-all-symbols"
fi
AC_SUBST(EXTRA_CPPFLAGS)
AC_SUBST(EXTRA_LDFLAGS)

# marshal and enum generators
GLIB_GENMARSHAL="`$PKG_CONFIG --variable=glib_genmarshal glib-2.0`"
AC_SUBST(GLIB_GENMARSHAL)
GLIB_MKENUMS="`$PKG_CONFIG --variable=glib_mkenums glib-2.0`"
AC_SUBST(GLIB_MKENUMS)

# i18n stuff
IT_PROG_INTLTOOL([0.40.4])

GETTEXT_PACKAGE=gdl-3
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])


dnl Set PREFIX
if test "x${prefix}" = "xNONE"; then
  packageprefix=${ac_default_prefix}
else
  packageprefix=${prefix}
fi

# images directory
gdl_images_dir="${packageprefix}/share/gdl/images"
AC_SUBST(gdl_images_dir)

# check for gtk-doc
GTK_DOC_CHECK(1.4)

GLIB_PREFIX="`$PKG_CONFIG --variable=prefix glib-2.0`"
GTK_PREFIX="`$PKG_CONFIG --variable=prefix gtk+-3.0`"

AC_SUBST(GLIB_PREFIX)
AC_SUBST(GTK_PREFIX)

GOBJECT_INTROSPECTION_CHECK([0.6.7])

AC_OUTPUT([
Makefile
gdl/Makefile
po/Makefile.in 
docs/Makefile
docs/reference/Makefile
docs/reference/version.xml
gdl.spec
gdl-3.0.pc
])
